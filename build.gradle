plugins {
    id "java"
    id "org.springframework.boot" version "2.4.5"
    id "org.liquibase.gradle" version "2.0.4"
}

repositories {
    jcenter()
    mavenCentral()

    maven {
        url = "https://jitpack.io"
    }

    maven {
        url = "https://m2.dv8tion.net/"
    }

    maven {
        url = "https://repo.jenkins-ci.org/releases/"
    }

    maven {
        name "m2-dv8tion"
        url "https://m2.dv8tion.net/releases"
    }
}

compileJava.options.encoding = "UTF-8"

dependencies {
    implementation "ch.qos.logback:logback-classic:1.2.3"

    implementation "com.antkorwin:xsync:1.3"
    implementation "com.google.apis:google-api-services-youtube:v3-rev20210915-1.32.1"
    implementation "com.google.guava:guava:31.0.1-jre"
    implementation "com.sedmelluq:jda-nas:1.1.0"
    implementation "com.github.walkyst:lavaplayer-fork:1.3.96"
    implementation "com.sedmelluq:lavaplayer-ext-youtube-rotator:0.2.3"

    implementation "commons-validator:commons-validator:1.7"

    implementation 'io.github.resilience4j:resilience4j-ratelimiter:1.7.1'

    implementation "net.dv8tion:JDA:4.3.0_346"
    implementation "net.robinfriedli:exec:1.2"
    implementation "net.robinfriedli:JXP:2.0.4"
    implementation "net.robinfriedli:StringList:1.6"
    implementation "net.robinfriedli:ThreadPool:1.1"
    implementation 'org.xerial:sqlite-jdbc:3.30.1'
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-text:1.9"
    implementation "org.codehaus.groovy:groovy-all:3.0.8"
    implementation "org.discordbots:DBL-Java-Library:2.0.1"
    implementation "org.ehcache:ehcache:3.9.7"
    implementation "org.glassfish:javax.el:3.0.1-b11"
    implementation "org.hibernate:hibernate-core:5.6.1.Final"
    implementation "org.hibernate:hibernate-entitymanager:5.6.1.Final"
    implementation "org.hibernate:hibernate-jcache:5.6.1.Final"
    implementation "org.hibernate:hibernate-validator:5.4.3.Final"
    implementation "org.hibernate:hibernate-validator-cdi:5.4.3.Final"
    implementation "org.liquibase:liquibase-core:4.6.1"
    implementation "org.postgresql:postgresql:42.3.1"
    implementation "org.postgresql:postgresql:42.3.1"
    implementation "org.quartz-scheduler:quartz:2.3.2"
    implementation "org.springframework.boot:spring-boot-dependencies:2.4.5"
    implementation "org.springframework.boot:spring-boot-starter-web:2.4.5"
    implementation "org.springframework.boot:spring-boot-starter-jdbc:2.4.5"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:2.4.5"
    implementation "org.springframework:spring-core:5.3.6"

    // check if IPlaylistItem implementations were added when upgrading and update SpotifyTrack accordingly
    implementation "se.michaelthelin.spotify:spotify-web-api-java:7.0.0"

    liquibaseRuntime "org.liquibase:liquibase-core:4.6.1"
    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate5:4.6.1"
    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:2.4.5"
}

group = "aiode"
version = "1.0-SNAPSHOT"
description = "aiode"
sourceCompatibility = "11"
targetCompatibility = "11"

jar {
    manifest {
        attributes(
            "Main-Class": "net.robinfriedli.aiode.boot.SpringBootstrap"
        )
    }
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/webapp"
        }
    }
}

def applicationProperties = new Properties()
file("src/main/resources/application.properties").withInputStream { applicationProperties.load(it) }
def privateProperties = new Properties()
file("src/main/resources/settings-private.properties").withInputStream { privateProperties.load(it) }

bootRun {
    jvmArgs("-Xmx" + applicationProperties.getProperty("aiode.preferences.max_heap_size"))
}

liquibase {
    activities {
        main {
            changeLogFile applicationProperties.getProperty("liquibase.change-log-path")
            referenceUrl applicationProperties.getProperty("liquibase.referenceUrl")
            url applicationProperties.getProperty("spring.datasource.url")
            username privateProperties.getProperty("spring.datasource.username")
            password privateProperties.getProperty("spring.datasource.password")
            referenceDriver "liquibase.ext.hibernate.database.connection.HibernateDriver"
        }
    }
}
